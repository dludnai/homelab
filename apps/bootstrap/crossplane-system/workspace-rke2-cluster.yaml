url: https://stevehipwell.github.io/helm-charts
chart: k8s-resources
version: 0.1.0
values:
  resources:
    - apiVersion: opentofu.upbound.io/v1beta1
      kind: Workspace
      name: rke2-cluster
      content:
        spec:
          providerConfigRef:
            name: default
          forProvider:
            env:
              - name: RANCHER_URL
                secretKeyRef:
                  namespace: crossplane-system
                  name: rancher-credentials
                  key: url
              - name: RANCHER_TOKEN_KEY
                secretKeyRef:
                  namespace: crossplane-system
                  name: rancher-credentials
                  key: token
              - name: TF_VAR_do_token
                secretKeyRef:
                  namespace: crossplane-system
                  name: do-credentials
                  key: token
            source: Inline
            module: |
              terraform {
                required_providers {
                  rancher2 = {
                    source  = "rancher/rancher2"
                    version = "~> 4.2.0"
                  }
                }
              }

              provider "rancher2" {}

              variable "do_token" {
                type      = string
                sensitive = true
              }

              variable "cluster_name" {
                type    = string
                default = "rke2-do-cluster"
              }

              variable "kubernetes_version" {
                type    = string
                default = "v1.32.4+rke2r1"
              }

              variable "region" {
                type    = string
                default = "fra1"
              }

              variable "droplet_size" {
                type    = string
                default = "s-4vcpu-8gb"
              }

              variable "cp_node_count" {
                type    = number
                default = 1
              }

              variable "worker_node_count" {
                type    = number
                default = 2
              }

              resource "rancher2_cloud_credential" "digitalocean" {
                name = "${var.cluster_name}-do-creds"
                digitalocean_credential_config {
                  access_token = var.do_token
                }
              }

              resource "rancher2_machine_config_v2" "control_plane" {
                generate_name = "${var.cluster_name}-cp"
                digitalocean_config {
                  image   = "ubuntu-22-04-x64"
                  region  = var.region
                  size    = var.droplet_size
                  tags    = "${var.cluster_name},control-plane"
                  backups = false
                  monitoring = true
                }
              }

              resource "rancher2_machine_config_v2" "worker" {
                generate_name = "${var.cluster_name}-worker"
                digitalocean_config {
                  image   = "ubuntu-22-04-x64"
                  region  = var.region
                  size    = var.droplet_size
                  tags    = "${var.cluster_name},worker"
                  backups = false
                  monitoring = true
                }
              }

              resource "rancher2_cluster_v2" "rke2" {
                name               = var.cluster_name
                kubernetes_version = var.kubernetes_version

                rke_config {
                  machine_pools {
                    name                         = "control-plane"
                    cloud_credential_secret_name = rancher2_cloud_credential.digitalocean.id
                    control_plane_role           = true
                    etcd_role                    = true
                    worker_role                  = false
                    quantity                     = var.cp_node_count
                    drain_before_delete          = true

                    machine_config {
                      kind = rancher2_machine_config_v2.control_plane.kind
                      name = rancher2_machine_config_v2.control_plane.name
                    }
                  }

                  machine_pools {
                    name                         = "worker"
                    cloud_credential_secret_name = rancher2_cloud_credential.digitalocean.id
                    control_plane_role           = false
                    etcd_role                    = false
                    worker_role                  = true
                    quantity                     = var.worker_node_count
                    drain_before_delete          = true

                    machine_config {
                      kind = rancher2_machine_config_v2.worker.kind
                      name = rancher2_machine_config_v2.worker.name
                    }
                  }
                }
              }

              output "cluster_id" {
                value = rancher2_cluster_v2.rke2.id
              }

              output "cluster_name" {
                value = rancher2_cluster_v2.rke2.name
              }
